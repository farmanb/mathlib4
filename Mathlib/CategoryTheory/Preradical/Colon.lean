/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module

public import Mathlib.CategoryTheory.Preradical.Basic
public import Mathlib.CategoryTheory.Preradical.Hom
public import Mathlib.CategoryTheory.Preradical.CokernelConstruction

/-!
# The colon construction on preradicals

Given preradicals `r` and `s` on an abelian category `C`, this file defines
their **colon** `r : s` in the sense of StenstrÃ¶m.  Categorically, `r : s` is
constructed objectwise as a pullback of the cokernel projection of `r` along
the inclusion of `s`.

This file is part of the `Preradical` hierarchy; see
`CategoryTheory/Preradical/Basic.lean` for an overview of the package.

## Main definitions

* `Preradical.colon_obj r s X` : The object `X` equipped with the pullback over `r.Ï€ X` and
  `s.Î¹ (r.coker X)`.
* `Preradical.colon_fst r s X` : The first projection `colon_obj r s X âŸ¶ X`.
* `Preradical.colon_snd r s X` : The second projection `colon_obj r s X âŸ¶ s (r.coker X)`.
* `Preradical.colon_map r s f` : The induced map `colon_obj r s X âŸ¶ colon_obj r s Y`
  for a morphism `f : X âŸ¶ Y`.
* `Preradical.colon r s : Preradical C` : The colon preradical, given objectwise by
  `colon_obj r s` and inclusion `colon_fst r s`.

## Tags

category_theory, preradical, colon, pullback
-/

@[expose] public section

open CategoryTheory
open CategoryTheory.Limits

variable {C : Type*} [Category C] [Abelian C]

namespace Preradical

/-- The object used to define the colon preradical `r : s` at an object `X`,
given by the pullback of `r.Ï€ X` along `s.Î¹ (r.coker X)`. -/
noncomputable
def colon_obj (r s : Preradical C) (X : C) : C :=
  pullback (r.Ï€ X) (s.Î¹ (r.coker X))

/-- The first projection from the colon object `colon_obj r s X` to `X`. -/
noncomputable
def colon_fst (r s : Preradical C) (X : C) : colon_obj r s X âŸ¶ X :=
  pullback.fst (r.Ï€ X) (s.Î¹ (r.coker X))

/-- The second projection from the colon object `colon_obj r s X` to `s (r.coker X)`. -/
noncomputable
def colon_snd (r s : Preradical C) (X : C) : colon_obj r s X âŸ¶ s (r.coker X) :=
  pullback.snd (r.Ï€ X) (s.Î¹ (r.coker X))

noncomputable
instance (r s : Preradical C) (X : C) : Mono (r.colon_fst s X) :=
  pullback.fst_of_mono

instance colon_snd_epi (r s : Preradical C) (X : C) : Epi (r.colon_snd s X) :=
  Abelian.epi_pullback_of_epi_f (r.Ï€ X) (s.Î¹ (r.coker X))

lemma colon.condition {r s : Preradical C} {X : C} :
    r.colon_fst s X â‰« r.Ï€ X = r.colon_snd s X â‰« s.Î¹ (r.coker X) :=
  pullback.condition

@[simp]
lemma Î¹_comp_f_comp_Ï€ (r : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    r.Î·.app X â‰« (f â‰« r.Ï€ Y) = 0 := by
  rw [r.Ï€_naturality f, â† Category.assoc, Î¹_comp_Ï€, zero_comp]

lemma colon_map_condition (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    r.colon_fst s X â‰« f â‰« (r.Ï€ Y) =
    (r.colon_snd s X â‰« s.map (r.coker_map f)) â‰« s.Î¹ (r.coker Y) := calc
  _ = r.colon_fst s X â‰« r.Ï€ X â‰« (r.coker_map f) := by
      rw [Ï€_naturality]
  _ = r.colon_snd s X â‰« s.Î¹ (r.coker X) â‰« (r.coker_map f) := by
      rw [â† Category.assoc, colon.condition, Category.assoc]
  _ = (r.colon_snd s X â‰« s.map (r.coker_map f)) â‰« s.Î¹ (r.coker Y) := by
      rw [s.Î¹_naturality (r.coker_map f), Category.assoc]

/-- The morphism on colon objects induced by a morphism `f : X âŸ¶ Y`. -/
noncomputable
def colon_map (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
colon_obj r s X âŸ¶ colon_obj r s Y :=
  pullback.map
        (fâ‚ := r.Ï€ X) (fâ‚‚ := s.Î¹ (r.coker X))
        (gâ‚ := r.Ï€ Y) (gâ‚‚ := s.Î¹ (r.coker Y))
        (iâ‚ := f)
        (iâ‚‚ := s.map (r.coker_map f))
        (iâ‚ƒ := r.coker_map f)
        (eqâ‚ := Eq.symm (Ï€_naturality r f))
        (eqâ‚‚ := Î¹_naturality s (r.coker_map f))

@[simp]
lemma colon_map_id (r s : Preradical C) (X : C) :
    r.colon_map s (ğŸ™ X) = ğŸ™ (colon_obj r s X) := by
  simp [colon_map]
  rfl

@[simp]
lemma colon_map_comp (r s : Preradical C) {L X Y : C} (f : L âŸ¶ X) (g : X âŸ¶ Y) :
    colon_map r s f â‰« colon_map r s g = colon_map r s (f â‰« g) := by
  apply pullback.hom_ext <;> simp [colon_map,Category.assoc]

@[reassoc]
lemma colon_map_fst (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    colon_map r s f â‰« r.colon_fst s Y = r.colon_fst s X â‰« f := by
  simp [colon_map, colon_fst]

@[simp, reassoc]
lemma colon_map_snd (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
     r.colon_snd s X â‰« s.map (r.cokernel_of.map f) = colon_map r s f â‰« r.colon_snd s Y := by
  simp [colon_map, colon_snd]

/-- The colon preradical `r : s` from StenstrÃ¶m, defined objectwise as
the pullback of `r.Ï€ X` along `s.Î¹ (r.coker X)`. -/
noncomputable
def colon (r s : Preradical C) : Preradical C where
  obj := fun X => colon_obj r s X
  map := fun f => colon_map r s f
  map_id := by simp only [colon_map_id, implies_true]
  map_comp := by
    intro L X Y f g
    apply pullback.hom_ext <;> simp [colon_map]
  Î· := {
    app := fun X => r.colon_fst s X
    naturality := fun X Y f => colon_map_fst r s f
  }
  mono_app := by infer_instance

@[simp]
lemma colon_fst_eq_Î·_app (r s : Preradical C) :
    âˆ€ X : C, (r.colon_fst s X) = (colon r s).Î·.app X :=
  fun _ => rfl

@[simp, reassoc]
lemma colon_fst_naturality (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    (r.colon s).Î¹ X â‰« f = (r.colon s).map f â‰« (r.colon s).Î¹ Y := by
  exact Î¹_naturality (r.colon s) f

@[simp]
lemma colon_snd_naturality (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
  r.colon_map s f â‰« r.colon_snd s Y = (r.colon s).map f â‰« r.colon_snd s Y := rfl

/-- For all `r s : Preradical C`, there is always a morphism `r X âŸ¶ r.colon s X`. -/
noncomputable
def toColon_app (r s : Preradical C) (X : C) : r X âŸ¶ (r.colon s) X := by
  refine pullback.lift (r.Î¹ X) 0 ?_
  simp only [coker_eq, Î¹_eq_app, Î¹_comp_Ï€, zero_comp]

/-- The canonical morphisms `r Î¹ X : r X âŸ¶ X` factor through
`r.toColon_app s X : r X âŸ¶ (r.colon s) X` via `(r.colon s).Î¹ X = r.colon_fst s X`. -/
@[simp, reassoc]
lemma toColon_app_comp_colon_fst (r s : Preradical C) (X : C) :
    r.toColon_app s X â‰« (r.colon s).Î·.app X = r.Î¹ X := by
  apply pullback.lift_fst

/-- By construction, `r.toColon_app s X â‰« r.colon_snd s X = 0`. -/
@[simp, reassoc]
lemma toColon_app_comp_colon_snd (r s : Preradical C) (X : C) :
    r.toColon_app s X â‰« r.colon_snd s X = 0 := by
  apply pullback.lift_snd

/-- The morphism `r.toColon_app X` is natural in `X`. -/
@[simp, reassoc]
lemma toColon_app_naturality (r s : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    r.map f â‰« r.toColon_app s Y = r.toColon_app s X â‰« (r.colon s).map f := by
  apply pullback.hom_ext
  Â· calc
    _ = (r.map f â‰« r.toColon_app s Y) â‰« (r.colon s).Î¹ Y := rfl
    _ = r.map f â‰« r.toColon_app s Y â‰« (r.colon s).Î¹ Y := by rw [Category.assoc]
    _ = r.map f â‰« r.Î¹ Y := by
      simp only [Î¹_eq_app, toColon_app_comp_colon_fst, NatTrans.naturality, Functor.id_obj,
        Functor.id_map]
    _ = r.Î¹ X â‰« f := by simp only [Î¹_eq_app, NatTrans.naturality, Functor.id_obj, Functor.id_map]
    _ = (r.toColon_app s X â‰« (r.colon s).Î¹ X )â‰« f := by
      simp only [Î¹_eq_app, toColon_app_comp_colon_fst]
    _ = r.toColon_app s X â‰« (r.colon s).Î¹ X â‰« f := by rw [Category.assoc]
    _ = r.toColon_app s X â‰« (r.colon s).map f â‰« (r.colon s).Î¹ Y := by
        rw [â†(r.colon s).Î¹_naturality f]
    _ = (r.toColon_app s X â‰« (r.colon s).map f )â‰« (r.colon s).Î¹ Y := by rw [Category.assoc]
    _ = (r.toColon_app s X â‰« (r.colon s).map f) â‰« pullback.fst (r.Ï€ Y) (s.Î¹ (r.coker Y)) :=
      rfl
  Â· calc
    _ = (r.map f â‰« r.toColon_app s Y) â‰« r.colon_snd s Y := rfl
    _ = r.map f â‰« r.toColon_app s Y â‰« r.colon_snd s Y := by simp
    _ = 0 := by simp
    _ = r.toColon_app s X â‰« r.colon_snd s X â‰« s.map (r.coker_map f) := by
      rw [â† Category.assoc, toColon_app_comp_colon_snd, zero_comp]
    _ = r.toColon_app s X â‰« (r.colon s).map f â‰« r.colon_snd s Y := by
      simp only [coker_eq, coker_map_eq, colon_map_snd, colon_snd_naturality]
    _ = (r.toColon_app s X â‰« (r.colon s).map f) â‰« r.colon_snd s Y := by
      rw [Category.assoc]
    _ = (r.toColon_app s X â‰« (r.colon s).map f) â‰« pullback.snd (r.Ï€ Y) (s.Î¹ (r.coker Y)) :=
      rfl

/-- The canonical morphism `r âŸ¶ r.colon s`.

Objectwise, this is the morphism `r.toColon_app s X : r X âŸ¶ (r.colon s) X`
induced by the universal property of the pullback defining the colon
preradical, factoring `r.Î¹ X` through the first projection
`(r.colon s).Î¹ X`. -/
noncomputable
def toColon (r s : Preradical C) : r âŸ¶ r.colon s where
  app := toColon_app r s
  naturality := fun {X Y} f => toColon_app_naturality r s f
  w := by
    ext X
    exact toColon_app_comp_colon_fst r s X

@[simp]
lemma zero_snd_of_zero_right {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (zero_right : IsZero Y) :
    pullback.snd f g = 0 :=
  IsZero.eq_zero_of_tgt zero_right _

@[simp]
lemma zero_condition_of_zero_right {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (zero_right : IsZero Y) :
    pullback.fst f g â‰« f = 0 :=
  by simp [pullback.condition,zero_right]

/-- In the pullback square
    P - - f' - - > Y
    |              |
    g'             g
    |              |
    v              V
    X - - f - - - >Z
  if `g = 0`, then `P â‰… kernel f`.
-/
noncomputable
def kernel_of_pullback_along_zero {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (zero_right : IsZero Y) :
    kernel f â‰… pullback f g := by
  exact {
    hom := pullback.lift (kernel.Î¹ f) 0 (by simp),
    inv := kernel.lift f (pullback.fst f g) (zero_condition_of_zero_right _ _ zero_right),
    hom_inv_id := by
      apply equalizer.hom_ext; simp
    inv_hom_id := by
      apply pullback.hom_ext <;> simp [zero_right]
  }

@[simp, reassoc]
lemma kernel_of_pullback_along_zero_hom_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
    (zero_right : IsZero Y) :
(kernel_of_pullback_along_zero f g zero_right).hom â‰« pullback.fst f g = kernel.Î¹ f := by
  simp [kernel_of_pullback_along_zero]

@[simp, reassoc]
lemma kernel_of_pullback_along_zero_inv_hom_kernel_Î¹ {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
    (zero_right : IsZero Y) :
    (kernel_of_pullback_along_zero f g zero_right).inv â‰« kernel.Î¹ f = pullback.fst f g := by
  simp [kernel_of_pullback_along_zero]

/-- If for all `X : C`, `s (r.coker X) = 0`, then `r.toColon s` is an isomorphism. -/
lemma isIso_toColon_of_kills_quotients (r s : Preradical C)
    (h : âˆ€ X : C, IsZero (s (r.coker X))) : IsIso (r.toColon s) := by
  refine Preradical.isIso_of_isIso_app (r.toColon s) ?_
  intro X

  let eâ‚ : kernel (r.Ï€ X) â‰… pullback (r.Ï€ X) (s.Î¹ (r.coker X)) :=
    kernel_of_pullback_along_zero (r.Ï€ X) (s.Î¹ (r.coker X)) (h X)
  let eâ‚‚ : r X â‰… kernel (r.Ï€ X) := r.kernelIso_Ï€ X

  let e : r X â‰… pullback (r.Ï€ X) (s.Î¹ (r.coker X)) := eâ‚‚ â‰ªâ‰« eâ‚

  have : (r.toColon s).app X = e.hom := by
    apply pullback.hom_ext
    Â· calc
      _ = (r.toColon s).app X â‰« (r.colon s).Î¹ X := rfl
      _ = r.Î¹ X := by simp
      _ = eâ‚‚.hom â‰« kernel.Î¹ (r.Ï€ X) := by rw [kernelIso_Ï€_hom_Î¹]
      _ = eâ‚‚.hom â‰« (eâ‚.hom â‰« r.colon_fst s X) := by
          rw [â†kernel_of_pullback_along_zero_hom_fst (r.Ï€ X) (s.Î¹ (r.coker X)) (h X)]
          rfl
      _ = eâ‚‚.hom â‰« eâ‚.hom â‰« r.colon_fst s X := by rw [â† Category.assoc]
      _ = (eâ‚‚.hom â‰« eâ‚.hom) â‰« r.colon_fst s X := by rw [â†Category.assoc]
      _ = (eâ‚‚.hom â‰« eâ‚.hom) â‰« (r.colon s).Î¹ X := by rfl
      _ = e.hom â‰« (r.colon s).Î¹ X := by
        have : eâ‚‚.hom â‰« eâ‚.hom = e.hom := rfl
        rw [this]
    Â· simp [IsZero.eq_zero_of_tgt (h X)]
  rw [this]
  infer_instance

end Preradical
