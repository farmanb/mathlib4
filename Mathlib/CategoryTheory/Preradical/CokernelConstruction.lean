/-
Copyright (c) 2026 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module

public import Mathlib.CategoryTheory.Preradical.Basic
public import Mathlib.CategoryTheory.Preradical.Hom

/-!
# The cokernel construction associated to a preradical

Given a preradical `r : Preradical C` on an abelian category `C`, this file
develops the functor `quotient r : C â¥¤ C` sending `X` to `X / r X`, the cokernel of the
structure morphism `r.Î¹ X : r X âŸ¶ X`.  We also construct the associated natural
projection `Ï€ r X : X âŸ¶ r.quotient.obj X` and prove the canonical isomorphism
`r X â‰… kernel (r.Ï€ X)`.

## References

* [Bo StenstrÃ¶m, *Rings and Modules of Quotients*][stenstrom1971]
* [Bo StenstrÃ¶m, *Rings of Quotients*][stenstrom1975]

## Tags

category theory, preradical, torsion theory
-/

@[expose] public section

open CategoryTheory
open CategoryTheory.Limits

variable {C : Type*} [Category C] [Abelian C]

namespace Preradical

variable (r : Preradical C)

/-- The cokernel of `r.Î· : r.toFunctor âŸ¶ ðŸ­ C`. -/
noncomputable abbrev quotient : C â¥¤ C := cokernel r.Î·

/-- The canonical projection onto the cokernel of `r.Î¹ X : r X âŸ¶ X`. -/
noncomputable def Ï€ (X : C) : X âŸ¶ r.quotient.obj X := (cokernel.Ï€ r.Î·).app X

@[simp]
lemma Ï€_def (X : C) : r.Ï€ X = (cokernel.Ï€ r.Î·).app X := rfl

@[simp, reassoc]
lemma Ï€_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (cokernel.Ï€ r.Î·).app Y = (cokernel.Ï€ r.Î·).app X â‰« r.quotient.map f := by
  exact (cokernel.Ï€ r.Î·).naturality f

/-- This lemma allows simp to automatically prove `r.Î¹ X â‰« r.Ï€ X = 0`. -/
@[simp, reassoc]
lemma Ï€_app_comp_map_Î· (X : C) : (cokernel.Ï€ r.Î·).app (r X) â‰« r.quotient.map (r.Î·.app X) = 0 := by
  rw [â† (cokernel.Ï€ r.Î·).naturality (r.Î·.app X)]
  exact Eq.trans (NatTrans.comp_app r.Î· (cokernel.Ï€ r.Î·) X)
    (congrArg (fun Î± => Î±.app X) (cokernel.condition r.Î·))

/-- The canonical isomorphism between the functorial cokernel `r.quotient.obj X` and the cokernel of
`r.Î¹ X`. -/
noncomputable
def quotientObjIso (X : C) : r.quotient.obj X â‰… cokernel (r.Î¹ X) := by
  simpa using (CategoryTheory.Limits.PreservesCokernel.iso
    ((CategoryTheory.evaluation C C).obj X) r.Î·)

lemma Ï€_comp_quotientObjIso (X : C) : r.Ï€ X â‰« (r.quotientObjIso X).hom = cokernel.Ï€ (r.Î¹ X) := by
  simpa [Preradical.Ï€, quotientObjIso, Preradical.Î¹]
    using (CategoryTheory.Limits.PreservesCokernel.Ï€_iso_hom
      (G := (CategoryTheory.evaluation C C).obj X) (f := r.Î·))

/-- The morphism `r.Ï€ X` exhibits `r.quotient.obj X` as the cokernel of `r.Î¹ X`. -/
noncomputable
def isCokernel_Ï€ (X : C) :
    IsColimit (CokernelCofork.ofÏ€ (r.Ï€ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)) := by
  let t  : CokernelCofork (r.Î¹ X) :=
    CokernelCofork.ofÏ€ (r.Ï€ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)
  let tâ‚€ : CokernelCofork (r.Î¹ X) :=
    CokernelCofork.ofÏ€ (cokernel.Ï€ (r.Î¹ X)) (cokernel.condition (r.Î¹ X))
  have e : t â‰… tâ‚€ :=
    { hom :=
        { hom := (r.quotientObjIso X).hom
          w := by
              intro j
              cases j
              Â· simp
              Â· simpa [t,tâ‚€] using Ï€_comp_quotientObjIso r X}
      inv :=
        { hom := (r.quotientObjIso X).inv
          w := by
            have h : t.Ï€ â‰« (r.quotientObjIso X).hom = tâ‚€.Ï€ := by
              simpa [t, tâ‚€] using (Ï€_comp_quotientObjIso (r := r) (X := X))
            have h' : (t.Ï€ â‰« (r.quotientObjIso X).hom) â‰« (r.quotientObjIso X).inv =
              tâ‚€.Ï€ â‰« (r.quotientObjIso X).inv := by
                simpa [Category.assoc] using congrArg (fun k => k â‰« (r.quotientObjIso X).inv) h
            intro j
            cases j <;> simp [h'.symm]}
      hom_inv_id := by ext; simp
      inv_hom_id := by ext; simp}
  exact IsColimit.ofIsoColimit (cokernelIsCokernel (r.Î¹ X)) e.symm

instance (X : C) : Epi (r.Ï€ X) := epi_of_isColimit_cofork (r.isCokernel_Ï€ X)

/-- The morphism `r.Î¹ X` exhibits `r X` as the kernel of `r.Ï€ X`. -/
noncomputable
def isKernel_Î¹ (X : C) : IsLimit (KernelFork.ofÎ¹ (r.Î¹ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)) :=
  Abelian.monoIsKernelOfCokernel _ (isCokernel_Ï€ r X)

/-- The canonical isomorphism r X â‰… kernel (r.Ï€ X). -/
noncomputable
def isoKernel_Ï€ (X : C) : r X â‰… kernel (r.Ï€ X) := by
  simpa using
    ( (isKernel_Î¹ (r := r) (X := X)).conePointUniqueUpToIso
        (kernelIsKernel (r.Ï€ X)) )

@[simp, reassoc]
lemma isoKernel_Ï€_hom_Î¹ (X : C) :
    (isoKernel_Ï€ r X).hom â‰« kernel.Î¹ ((cokernel.Ï€ r.Î·).app X) = r.Î¹ X := by
  simpa [isoKernel_Ï€] using
    (IsLimit.conePointUniqueUpToIso_hom_comp
      (isKernel_Î¹ (r := r) (X := X))
      (kernelIsKernel (r.Ï€ X))
      WalkingParallelPair.zero)

end Preradical
