/-
Copyright (c) 2026 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module

public import Mathlib.CategoryTheory.Preradical.Basic

/-!
# The cokernel construction associated to a preradical

Given a preradical `Î¦ : Preradical C` on an abelian category `C`, this file develops the functor
`quotient Î¦ : C â¥¤ C` sending `X` to the cokernel of `Î¦.Î¹.app X : Î¦.r.obj X âŸ¶ X`.  We also construct
the associated natural projection `Ï€ Î¦ X : X âŸ¶ Î¦.quotient.obj X` and prove the canonical
isomorphism `Î¦.r.obj X â‰… kernel (Î¦.Ï€ X)`.

## References

* [Bo StenstrÃ¶m, *Rings and Modules of Quotients*][stenstrom1971]
* [Bo StenstrÃ¶m, *Rings of Quotients*][stenstrom1975]

## Tags

category theory, preradical, torsion theory
-/

@[expose] public section

namespace CategoryTheory.Abelian
open CategoryTheory.Limits

variable {C : Type*} [Category C] [Abelian C]

namespace Preradical

variable (Î¦ : Preradical C)

/-- The cokernel of `Î¦.Î¹ : Î¦.r âŸ¶ ðŸ­ C`. -/
noncomputable abbrev quotient : C â¥¤ C := cokernel Î¦.Î¹

/-- The canonical projection onto the cokernel of `Î¦.Î¹.app X : Î¦.r.obj X âŸ¶ X`. -/
noncomputable def Ï€ (X : C) : X âŸ¶ Î¦.quotient.obj X := (cokernel.Ï€ Î¦.Î¹).app X

@[simp]
lemma Ï€_def (X : C) : Î¦.Ï€ X = (cokernel.Ï€ Î¦.Î¹).app X := rfl

@[simp, reassoc]
lemma Ï€_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (cokernel.Ï€ Î¦.Î¹).app Y = (cokernel.Ï€ Î¦.Î¹).app X â‰« Î¦.quotient.map f := by
  exact (cokernel.Ï€ Î¦.Î¹).naturality f

@[simp, reassoc]
lemma Ï€_app_comp_map_Î¹ (X : C) :
    (cokernel.Ï€ Î¦.Î¹).app (Î¦.r.obj X) â‰« Î¦.quotient.map (Î¦.Î¹.app X) = 0 := by
  rw [â† (cokernel.Ï€ Î¦.Î¹).naturality (Î¦.Î¹.app X)]
  exact Eq.trans (NatTrans.comp_app Î¦.Î¹ (cokernel.Ï€ Î¦.Î¹) X)
    (congrArg (fun Î± => Î±.app X) (cokernel.condition Î¦.Î¹))

/-- The canonical isomorphism between the functorial cokernel `Î¦.quotient.obj X` and the cokernel
of `Î¦.Î¹.app X`. -/
noncomputable
def quotientObjIso (X : C) : Î¦.quotient.obj X â‰… cokernel (Î¦.Î¹.app X) := by
  simpa using (CategoryTheory.Limits.PreservesCokernel.iso
    ((CategoryTheory.evaluation C C).obj X) Î¦.Î¹)

lemma Ï€_comp_quotientObjIso (X : C) :
    Î¦.Ï€ X â‰« (Î¦.quotientObjIso X).hom = cokernel.Ï€ (Î¦.Î¹.app X) := by
  simpa [Preradical.Ï€, quotientObjIso, Preradical.Î¹]
    using (CategoryTheory.Limits.PreservesCokernel.Ï€_iso_hom
      (G := (CategoryTheory.evaluation C C).obj X) (f := Î¦.Î¹))

/-- The morphism `Î¦.Ï€ X` exhibits `Î¦.quotient.obj X` as the cokernel of `Î¦.Î¹.app X`. -/
noncomputable
def isCokernel_Ï€ (X : C) :
    IsColimit (CokernelCofork.ofÏ€ (Î¦.Ï€ X) (show Î¦.Î¹.app X â‰« Î¦.Ï€ X = 0 by simp)) := by
  let t  : CokernelCofork (Î¦.Î¹.app X) :=
    CokernelCofork.ofÏ€ (Î¦.Ï€ X) (show Î¦.Î¹.app X â‰« Î¦.Ï€ X = 0 by simp)
  let tâ‚€ : CokernelCofork (Î¦.Î¹.app X) :=
    CokernelCofork.ofÏ€ (cokernel.Ï€ (Î¦.Î¹.app X)) (cokernel.condition (Î¦.Î¹.app X))
  have e : t â‰… tâ‚€ :=
    { hom :=
        { hom := (Î¦.quotientObjIso X).hom
          w := by
              intro j
              cases j
              Â· simp
              Â· simpa [t,tâ‚€] using Ï€_comp_quotientObjIso Î¦ X}
      inv :=
        { hom := (Î¦.quotientObjIso X).inv
          w := by
            have h : t.Ï€ â‰« (Î¦.quotientObjIso X).hom = tâ‚€.Ï€ := by
              simpa [t, tâ‚€] using (Ï€_comp_quotientObjIso (Î¦ := Î¦) (X := X))
            have h' : (t.Ï€ â‰« (Î¦.quotientObjIso X).hom) â‰« (Î¦.quotientObjIso X).inv =
              tâ‚€.Ï€ â‰« (Î¦.quotientObjIso X).inv := by
                simpa [Category.assoc] using congrArg (fun k => k â‰« (Î¦.quotientObjIso X).inv) h
            intro j
            cases j <;> simp [h'.symm]}
      hom_inv_id := by ext; simp
      inv_hom_id := by ext; simp}
  exact IsColimit.ofIsoColimit (cokernelIsCokernel (Î¦.Î¹.app X)) e.symm

instance (X : C) : Epi (Î¦.Ï€ X) := epi_of_isColimit_cofork (Î¦.isCokernel_Ï€ X)

/-- The morphism `Î¦.Î¹.app X` exhibits `Î¦.r.obj X` as the kernel of `Î¦.Ï€ X`. -/
noncomputable
def isKernel_Î¹ (X : C) :
    IsLimit (KernelFork.ofÎ¹ (Î¦.Î¹.app X) (show Î¦.Î¹.app X â‰« Î¦.Ï€ X = 0 by simp)) :=
  Abelian.monoIsKernelOfCokernel _ (Î¦.isCokernel_Ï€ X)

/-- The canonical isomorphism `Î¦.r.obj X â‰… kernel (Î¦.Ï€ X)`. -/
noncomputable
def isoKernel_Ï€ (X : C) : Î¦.r.obj X â‰… kernel (Î¦.Ï€ X) := by
  simpa using
    ( (isKernel_Î¹ (Î¦ := Î¦) (X := X)).conePointUniqueUpToIso
        (kernelIsKernel (Î¦.Ï€ X)) )

@[simp, reassoc]
lemma isoKernel_Ï€_hom_Î¹ (X : C) :
    (Î¦.isoKernel_Ï€ X).hom â‰« kernel.Î¹ ((cokernel.Ï€ Î¦.Î¹).app X) = Î¦.Î¹.app X := by
  simpa [isoKernel_Ï€] using
    (IsLimit.conePointUniqueUpToIso_hom_comp
      (isKernel_Î¹ (Î¦ := Î¦) (X := X))
      (kernelIsKernel (Î¦.Ï€ X))
      WalkingParallelPair.zero)

end Preradical

end CategoryTheory.Abelian
