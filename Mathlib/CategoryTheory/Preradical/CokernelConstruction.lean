/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module

public import Mathlib.CategoryTheory.Preradical.Basic
public import Mathlib.CategoryTheory.Preradical.Hom

/-!
# The cokernel construction associated to a preradical

Given a preradical `r : Preradical C` on an abelian category `C`, this file
develops the functor `coker r : C â¥¤ C` sending `X` to the cokernel of the
structure morphism `r.Î¹ X : r X âŸ¶ X`.  We also construct the associated natural
projection `Ï€ r X : X âŸ¶ r.coker.obj X` and prove the canonical isomorphism `r X â‰… kernel (r.Ï€ X)`.

This comparison isomorphism expresses categorically that a preradical embeds each
object as the kernel of the corresponding cokernel projection.
-/

@[expose] public section

open CategoryTheory
open CategoryTheory.Limits

variable {C : Type*} [Category C] [Abelian C]

namespace Preradical

/-- The cokernel of `r.Î· : r.toFunctor âŸ¶ ðŸ­ C`. -/
noncomputable abbrev coker (r : Preradical C) : C â¥¤ C := cokernel r.Î·

/-- The canonical isomorphism between the functorial cokernel
`r.coker.obj X` and the cokernel of `r.Î¹ X`. -/
noncomputable
def cokerObjIso (r : Preradical C) (X : C) : r.coker.obj X â‰… cokernel (r.Î¹ X) := by
  simpa [Preradical.coker, Preradical.Î¹] using (CategoryTheory.Limits.PreservesCokernel.iso
    ((CategoryTheory.evaluation C C).obj X) r.Î·)

/-- The projection `ðŸ­ C âŸ¶ r.coker`. -/
noncomputable abbrev coker_Ï€ (r : Preradical C) : ðŸ­ C âŸ¶ r.coker := cokernel.Ï€ r.Î·

/-- The canonical projection onto the cokernel of `r.Î¹ X : r X âŸ¶ X`. -/
noncomputable def Ï€ (r : Preradical C) (X : C) : X âŸ¶ r.coker.obj X := r.coker_Ï€.app X

@[simp]
lemma Ï€_def (r : Preradical C) (X : C) : r.Ï€ X = (cokernel.Ï€ r.Î·).app X := rfl

@[simp, reassoc]
lemma Ï€_naturality (r : Preradical C) {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (cokernel.Ï€ r.Î·).app Y = (cokernel.Ï€ r.Î·).app X â‰« r.coker.map f := by
  exact (cokernel.Ï€ r.Î·).naturality f

/-- The simpNF for `r.Î·.app X â‰« (cokernel.Ï€ r.Î·).app X = 0`.
     (cokernel.Ï€ r.Î·).app (r X)
    r X - - - - - - - - - - - - -> r.coker.obj (r X)
     |                                 |
     | r.Î·.app X                       | r.coker.map (r.Î· X)
     v                                 v
     X - - - - - - - - - - - - - > r.coker.obj X
       (cokernel.Ï€ r.Î·).app X
-/
@[simp, reassoc]
lemma Î·_app_comp_coker_Ï€_app (r : Preradical C) (X : C) :
    (cokernel.Ï€ r.Î·).app (r X) â‰« r.coker.map (r.Î·.app X) = 0 := by
  rw[â†(cokernel.Ï€ r.Î·).naturality (r.Î·.app X)]
  exact Eq.trans (NatTrans.comp_app r.Î· (cokernel.Ï€ r.Î·) X)
    (congrArg (fun Î± => Î±.app X) (cokernel.condition r.Î·))

lemma Ï€_comp_cokerObjIso_hom (r : Preradical C) (X : C) :
    r.Ï€ X â‰« (r.cokerObjIso X).hom = cokernel.Ï€ (r.Î¹ X) := by
  simpa [Preradical.Ï€, Preradical.coker_Ï€, cokerObjIso, Preradical.Î¹]
    using (CategoryTheory.Limits.PreservesCokernel.Ï€_iso_hom
      (G := (CategoryTheory.evaluation C C).obj X) (f := r.Î·))

/-- The morphism `r.Ï€ X` exhibits `r.coker.obj X` as the cokernel of `r.Î¹ X`. -/
noncomputable
def isCokernel_Ï€ (r : Preradical C) (X : C) :
    IsColimit (CokernelCofork.ofÏ€ (r.Ï€ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)) := by
  let t  : CokernelCofork (r.Î¹ X) :=
    CokernelCofork.ofÏ€ (r.Ï€ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)
  let tâ‚€ : CokernelCofork (r.Î¹ X) :=
    CokernelCofork.ofÏ€ (cokernel.Ï€ (r.Î¹ X)) (cokernel.condition (r.Î¹ X))
  have e : t â‰… tâ‚€ := by
    refine
      { hom :=
          { hom := (r.cokerObjIso X).hom
            w := ?_ }
        inv :=
          { hom := (r.cokerObjIso X).inv
            w := ?_ }
        hom_inv_id := by ext; simp
        inv_hom_id := by ext; simp }
    Â· intro j
      cases j
      Â· simp
      Â· simpa [t, tâ‚€] using Ï€_comp_cokerObjIso_hom r X
    Â· have h : t.Ï€ â‰« (r.cokerObjIso X).hom = tâ‚€.Ï€ := by
        simpa [t, tâ‚€] using (Ï€_comp_cokerObjIso_hom (r := r) (X := X))
      have h' : (t.Ï€ â‰« (r.cokerObjIso X).hom) â‰« (r.cokerObjIso X).inv =
          tâ‚€.Ï€ â‰« (r.cokerObjIso X).inv := by
        simpa [Category.assoc] using congrArg (fun k => k â‰« (r.cokerObjIso X).inv) h
      intro j
      cases j <;> simp [h'.symm]
  exact IsColimit.ofIsoColimit (cokernelIsCokernel (r.Î¹ X)) e.symm

instance (r : Preradical C) (X : C) : Epi (r.Ï€ X) := epi_of_isColimit_cofork (r.isCokernel_Ï€ X)

/-- The morphism `r.Î¹ X` exhibits `r X` as the kernel of `r.Ï€ X`. -/
noncomputable
def isKernel_Î¹_of_Ï€ (r : Preradical C) (X : C) :
    IsLimit (KernelFork.ofÎ¹ (r.Î¹ X) (show r.Î¹ X â‰« r.Ï€ X = 0 by simp)) :=
  Abelian.monoIsKernelOfCokernel _ (isCokernel_Ï€ r X)

/-- The canonical isomorphism r X â‰… kernel (r.Ï€ X). -/
noncomputable
def kernelIso_Ï€ (r : Preradical C) (X : C) : r X â‰… kernel (r.Ï€ X) := by
  simpa using
    ( (isKernel_Î¹_of_Ï€ (r := r) (X := X)).conePointUniqueUpToIso
        (kernelIsKernel (r.Ï€ X)) )

@[simp, reassoc]
lemma kernelIso_Ï€_hom_Î¹ (r : Preradical C) (X : C) :
    (kernelIso_Ï€ r X).hom â‰« kernel.Î¹ ((cokernel.Ï€ r.Î·).app X) = r.Î¹ X := by
  simpa [kernelIso_Ï€] using
    (IsLimit.conePointUniqueUpToIso_hom_comp
      (isKernel_Î¹_of_Ï€ (r := r) (X := X))
      (kernelIsKernel (r.Ï€ X))
      WalkingParallelPair.zero)

end Preradical
