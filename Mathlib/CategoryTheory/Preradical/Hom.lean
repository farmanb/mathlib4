/-
Copyright (c) 2024 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
import Mathlib.CategoryTheory.Preradical.Basic

/-!
# Morphisms of preradicals

This file develops the morphisms between preradicals on an abelian category `C` and equips
`Preradical C` with a category structure.

A morphism of preradicals `Î¼ : r âŸ¶ s` consists of a natural transformation
`Î¼.toNatTrans : r.F âŸ¶ s.F` whose components commute with the structure
morphisms `r.Î¹` and `s.Î¹`, in the sense that each square

r X â€” Î¼.app X â€“> s X
|                |
r.Î¹ X            s.Î¹ X
|                |
v                v
X  â€“â€“  ğŸ™ X  â€”â€“>  X

commutes.

This file is part of the `Preradical` hierarchy; see
`CategoryTheory/Preradical/Basic.lean` for an overview of the entire package.
-/

open CategoryTheory
open CategoryTheory.Limits

universe u v

variable {C : Type u} [Category.{v} C] [Abelian C]

namespace Preradical

structure Hom (r s : Preradical C) extends (r.F âŸ¶ s.F) where
  w : toNatTrans â‰« s.Î· = r.Î·

@[simp] lemma Hom.app_naturality {r s : Preradical C} (Î¼ : Preradical.Hom r s)
{X Y : C} (f : X âŸ¶ Y) : r.map f â‰« Î¼.app Y = Î¼.app X â‰« s.map f := Î¼.naturality f

@[ext]
lemma Hom.ext {r s : Preradical C} {f g : Hom r s} (h : f.toNatTrans = g.toNatTrans) : f = g := by
  cases f; cases g; cases h; rfl

instance : Category (Preradical C) where
  Hom := Hom
  id := fun r => Hom.mk (ğŸ™ r.F) (Category.id_comp r.Î·)
  comp {r s t} Î¼ Î½ :=
    Hom.mk (Î¼.toNatTrans â‰« Î½.toNatTrans : r.F âŸ¶ t.F) (by simp[Î½.w,Î¼.w])
  id_comp := by simp
  comp_id := by simp
  assoc := by simp

@[simp]
lemma Hom.w_app {r s : Preradical C} (Î¼ : r âŸ¶ s) (X : C) :
Î¼.app X â‰« s.Î¹ X = r.Î¹ X := congrArg (fun (Î½ : r.F âŸ¶ ğŸ­ C) => Î½.app X) Î¼.w

@[ext]
lemma ext_hom {r s : Preradical C} {Î¼ Î½ : r âŸ¶ s}
(h : Î¼.toNatTrans = Î½.toNatTrans) : Î¼ = Î½ := Preradical.Hom.ext h

@[simp, reassoc]
lemma Hom.comp_app {r s t : Preradical C}
(Î¼ : r âŸ¶ s) (Î½ : Hom s t) (X : C) : (Î¼ â‰« Î½).app X = Î¼.app X â‰« Î½.app X := by rfl

theorem epi_of_epi_app {r s : Preradical C} (Î¼ : r âŸ¶ s) [h_Î¼ : âˆ€ X : C, Epi (Î¼.app X)] : Epi Î¼ where
  left_cancellation := by
    intro t _ _ h_comp
    ext X
    exact (cancel_epi (Î¼.app X)).mp (by simp[â† Hom.comp_app,h_comp])

theorem mono_of_mono_app {r s : Preradical C} (Î¼ : r âŸ¶ s) [h_Î¼ : âˆ€ X : C, Mono (Î¼.app X)] :
Mono Î¼ where
  right_cancellation := by
    intro t _ _ h_comp
    ext X
    exact (cancel_mono (Î¼.app X)).mp (by simp[â†Hom.comp_app,h_comp])

theorem iso_of_iso_app {r s : Preradical C} (Î¼ : r âŸ¶ s) (h_Î¼ : âˆ€ X : C, IsIso (Î¼.app X)) :
IsIso Î¼ where
  out := by
    let Î½ : s âŸ¶ r := {
      app := fun X => inv (Î¼.app X)
      naturality := by
        intro X Y f
        apply (cancel_epi (Î¼.app X)).mp
        simp[â†Category.assoc,â†Hom.app_naturality]
      w := by
        ext X
        simp
    }
    use Î½
    constructor <;> (ext; simp[Î½]; rfl)
end Preradical
