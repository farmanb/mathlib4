/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module
public import Mathlib.CategoryTheory.Preradical.Basic

/-!
# Morphisms of preradicals

This file develops the morphisms between preradicals on an abelian category `C` and equips
`Preradical C` with a category structure.

A morphism of preradicals `Œº : r ‚ü∂ s` consists of a natural transformation
`Œº.toNatTrans : r.F ‚ü∂ s.F` whose components commute with the structure
morphisms `r.Œπ` and `s.Œπ`, in the sense that each square

r X ‚Äî Œº.app X ‚Äì> s X
|                |
r.Œπ X            s.Œπ X
|                |
v                v
X  ‚Äì‚Äì  ùüô X  ‚Äî‚Äì>  X

commutes.

This file is part of the `Preradical` hierarchy; see
`CategoryTheory/Preradical/Basic.lean` for an overview of the entire package.
-/

@[expose] public section

open CategoryTheory

variable {C : Type*} [Category C] [Abelian C]

namespace Preradical

/-- A morphism of preradicals `Œº : r ‚ü∂ s` is a natural transformation `r.F ‚ü∂ s.F`
whose components are compatible with the structure maps in the sense that the square
r X ‚Äî Œº.app X ‚Äì> s X
|                |
r.Œπ X            s.Œπ X
|                |
v                v
X  ‚Äì‚Äì  ùüô X  ‚Äî‚Äì>  X
commutes. -/
structure Hom (r s : Preradical C) extends (r.toFunctor ‚ü∂ s.toFunctor) where
  w : toNatTrans ‚â´ s.Œ∑ = r.Œ∑

instance : Category (Preradical C) where
  Hom := Hom
  id := fun r => Hom.mk (ùüô r.toFunctor) (Category.id_comp r.Œ∑)
  comp {r s t} Œº ŒΩ :=
    Hom.mk (Œº.toNatTrans ‚â´ ŒΩ.toNatTrans : r.toFunctor ‚ü∂ t.toFunctor) (by simp [ŒΩ.w, Œº.w])
  id_comp := by simp
  comp_id := by simp
  assoc := by simp

namespace Hom

@[simp]
lemma w_app {r s : Preradical C} (Œº : r ‚ü∂ s) (X : C) :
    Œº.app X ‚â´ s.Œ∑.app X = r.Œ∑.app X := by
  exact congrArg (fun (ŒΩ : r.toFunctor ‚ü∂ ùü≠ C) => ŒΩ.app X) Œº.w
  --simpa [Preradical.Œπ] using (congrArg (fun (ŒΩ : r.toFunctor ‚ü∂ ùü≠ C) => ŒΩ.app X) Œº.w)

@[ext]
lemma ext {r s : Preradical C} {Œº ŒΩ : r ‚ü∂ s} (h : Œº.toNatTrans = ŒΩ.toNatTrans) :
    Œº = ŒΩ := by
  cases Œº; cases ŒΩ; cases h; rfl

@[simp, reassoc]
lemma comp_app {r s t : Preradical C} (Œº : r ‚ü∂ s) (ŒΩ : s ‚ü∂ t) (X : C) :
    (Œº ‚â´ ŒΩ).app X = Œº.app X ‚â´ ŒΩ.app X := by
  rfl

theorem epi_of_epi_app {r s : Preradical C} (Œº : r ‚ü∂ s) [hŒº : ‚àÄ X : C, Epi (Œº.app X)] :
    Epi Œº where
  left_cancellation := by
    intro t _ _ hcomp
    ext X
    exact (cancel_epi (Œº.app X)).mp (by simp [‚Üê Hom.comp_app, hcomp])

theorem mono_of_mono_app {r s : Preradical C} (Œº : r ‚ü∂ s) [hŒº : ‚àÄ X : C, Mono (Œº.app X)] :
    Mono Œº where
  right_cancellation := by
    intro t _ _ hcomp
    ext X
    exact (cancel_mono (Œº.app X)).mp (by simp [‚Üê Hom.comp_app, hcomp])

theorem isIso_of_isIso_app {r s : Preradical C} (Œº : r ‚ü∂ s) (hŒº : ‚àÄ X : C, IsIso (Œº.app X)) :
    IsIso Œº := by
  letI : IsIso (C := C ‚•§ C) (Œº.toNatTrans : r.toFunctor ‚ü∂ s.toFunctor) :=
    NatIso.isIso_of_isIso_app Œº.toNatTrans
  refine ‚ü®?_, ?_‚ü©
  ¬∑ exact {
    app := (inv (C := C ‚•§ C) (Œº.toNatTrans : r.toFunctor ‚ü∂ s.toFunctor)).app
    naturality := by
      intro X Y f
      apply (cancel_epi (Œº.app X)).1
      simp only [NatIso.isIso_inv_app, NatIso.naturality_2', IsIso.hom_inv_id_assoc]
    w := by
      ext X
      simp only [Functor.id_obj, NatTrans.comp_app, NatIso.isIso_inv_app, IsIso.inv_comp_eq,
        Hom.w_app]}
  ¬∑ constructor <;>
      ext X <;>
      simp only [Hom.comp_app, NatIso.isIso_inv_app, IsIso.hom_inv_id,IsIso.inv_hom_id] <;>
      rfl

end Hom

end Preradical
