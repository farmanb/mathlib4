/-
Copyright (c) 2024 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/

import Mathlib.CategoryTheory.Abelian.Basic
import Mathlib.CategoryTheory.Abelian.Opposite

import Mathlib.CategoryTheory.Category.Basic

import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.Comma.Basic

/-!
# Functorial Factorization Systems

For a category, `C`, a functorial factorization consists of two endofunctors
`L,R : Arrow C â¥¤ Arrow C` that satisfy the condition for every arrow `f : X âŸ¶ Y` there exists
an intermediate object `I` of `C` such that the arrows `L f : X âŸ¶ I` and `R f : I â†’ Y` compose to
`f`.

In order to make this construction more natural, we regard a `FunctorialFactorization` as a
functor `Arrow C â†’ Comma Arrow.rightFunc Arrow.leftFunc` defined by mapping `f : Arrow C` to the
triple `(L f, R f, h)`, where `h` is an isomorphism between the intermediate objects.

## Main definitions

* `FunctorialFactorization C`: a functorial factorization system on a category `C`.

## References

* [Mark Hovey, Model Categories][hovey-1999]
* https://ncatlab.org/nlab/show/functorial+factorization

## Tags

functorial factorization, factorization system, arrow category, comma category, category theory,
model categories
-/
namespace CategoryTheory
open Category

universe v u

-- morphism levels before object levels. See note [CategoryTheory universes].
variable {C : Type u} [Category.{v} C]

section

variable (C)

/-- A functorial factorization, regarded as a functor
`Arrow T â¥¤ Comma Arrow.rightFunc Arrow.leftFunc`.
-/
structure FunctorialFactorization where
  factor : Arrow C â¥¤ Comma (Arrow.rightFunc : Arrow C â¥¤ C) (Arrow.leftFunc : Arrow C â¥¤ C)

  factor_left_obj : âˆ€ X : Arrow C, (factor.obj X).left.left = X.left
  factor_right_obj : âˆ€ X : Arrow C, (factor.obj X).right.right = X.right

  factor_middle_iso : âˆ€ X : Arrow C, IsIso (factor.obj X).hom

  factorization_spec : âˆ€ X : Arrow C,
    --inv (eqToHom (factor_left_obj X)) â‰«
    eqToHom ((factor_left_obj X).symm) â‰«
    (factor.obj X).left.hom â‰«
    (factor.obj X).hom â‰«
    (factor.obj X).right.hom â‰«
    eqToHom (factor_right_obj X) = X.hom
end

namespace FunctorialFactorization
open Abelian
open Limits

def factor_left (F : FunctorialFactorization C) : Arrow C â¥¤ Arrow C :=
  F.factor â‹™ Comma.fst (Arrow.rightFunc) (Arrow.leftFunc)

def factor_right (F : FunctorialFactorization C) : Arrow C â¥¤ Arrow C :=
  F.factor â‹™ Comma.snd (Arrow.rightFunc) (Arrow.leftFunc)

@[simp]
lemma intermediate_obj_iso (F : FunctorialFactorization C) (f : Arrow C) :
  IsIso (F.factor.obj f).hom := F.factor_middle_iso f

section images
/-- The image inclusion, followed by the right leg of a morphism in `Arrow C`,
  and then the cokernel projection, is zero. -/
@[simp]
private lemma image_to_cokernel_comp_eq_zero [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  (Abelian.image.Î¹ X.hom â‰« f.right) â‰« cokernel.Ï€ Y.hom = 0 := by
    rw[â† cancel_epi (coimage.Ï€ X.hom â‰« coimageImageComparison X.hom)]
    calc
      (coimage.Ï€ X.hom â‰« coimageImageComparison X.hom) â‰«
      (Abelian.image.Î¹ X.hom â‰« f.right) â‰«
      cokernel.Ï€ Y.hom
    _= coimage.Ï€ X.hom â‰« coimageImageComparison X.hom â‰«
      Abelian.image.Î¹ X.hom â‰« f.right â‰«
      cokernel.Ï€ Y.hom := by rw[assoc,assoc]
    _= X.hom â‰« f.right â‰« cokernel.Ï€ Y.hom := by
      simp only[â† assoc,coimage_image_factorisation X.hom]
    _= f.left â‰« Y.hom â‰« cokernel.Ï€ Y.hom := by
      rw[â†assoc,â† Arrow.w f,assoc]
    _= f.left â‰« 0 := by rw[cokernel.condition Y.hom]
    _= 0 := HasZeroMorphisms.comp_zero f.left (cokernel Y.hom)
    _= (coimage.Ï€ X.hom â‰« coimageImageComparison X.hom) â‰« 0 := by
      exact Eq.symm (HasZeroMorphisms.comp_zero
          (coimage.Ï€ X.hom â‰« coimageImageComparison X.hom)
          (cokernel Y.hom))

/-- Assume `f : X âŸ¶ Y` is a morphism in an abelian category. If `z : Z âŸ¶ X` satisfies the
  condition that `z â‰« f = 0`, then `z = kernel.lift f z h_z`.
-/
@[simp]
private lemma kernel.lift_unique [Abelian C] {X Y Z : C} {f : X âŸ¶ Y}
  (z : Z âŸ¶ X)
  (h_z : z â‰« f = 0)
  (w : Z âŸ¶ kernel f)
  (h_w : w â‰« kernel.Î¹ f = z) : w = kernel.lift f z h_z :=
    (kernelIsKernel f).uniq (KernelFork.ofÎ¹ z h_z) w (by rintro (_ | _) <;> simp[h_z,h_w])


noncomputable def image.map [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  Abelian.image X.hom âŸ¶ Abelian.image Y.hom :=
    kernel.map
    (cokernel.Ï€ X.hom)
    (cokernel.Ï€ Y.hom) f.right
    (cokernel.map X.hom Y.hom f.left f.right (Arrow.w f).symm)
    (cokernel.Ï€_desc X.hom (f.right â‰« cokernel.Ï€ Y.hom)
      (cokernel.map._proof_1 X.hom Y.hom f.left f.right (Eq.symm (Arrow.w f))))


@[simp]
private lemma image.naturality [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  image.map f â‰« Abelian.image.Î¹ Y.hom = Abelian.image.Î¹ X.hom â‰« f.right :=
    kernel.lift_Î¹ _ _ _

private lemma image.map_unique [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y)
  (g : Abelian.image X.hom âŸ¶ Abelian.image Y.hom)
  (h_f : g â‰« Abelian.image.Î¹ Y.hom = Abelian.image.Î¹ X.hom â‰« f.right) : g = image.map f :=
    kernel.lift_unique _ _ _ h_f

@[simp]
private lemma image_id_eq_id_image [Abelian C] (X : Arrow C) :
  ğŸ™ (Abelian.image X.hom) = image.map (ğŸ™ X) := by
    ext; simp

@[simp]
private lemma image.map_comp [Abelian C] {X Y Z : Arrow C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
 image.map f â‰« image.map g = image.map (f â‰« g) := by
  apply image.map_unique (f â‰« g)
  simp [Eq.symm (assoc (image.map f) (Abelian.image.Î¹ Y.hom) g.right)]

noncomputable def imFunc [Abelian C] : Arrow C â¥¤ Arrow C where
  obj := fun X : Arrow C => Arrow.mk (Abelian.image.Î¹ X.hom)
  map := fun {_ _} f => Arrow.homMk' (image.map f) f.right
  map_id := by intro; ext <;> simp
  map_comp := by
    intro X Y Z f g
    ext <;> simp

@[simp]
private lemma imFunc_obj_hom_eq [Abelian C] (X : Arrow C) :
   (imFunc.obj X).hom = Abelian.image.Î¹ X.hom := rfl

@[simp]
private lemma imFunc_map_right_eq [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  (imFunc.map f).right = f.right := rfl

@[simp]
private lemma imFunc_map_left_eq [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  (Arrow.leftFunc : Arrow C â¥¤ C).map (imFunc.map f) = image.map f := rfl

noncomputable def id_to_imFunc [Abelian C] : ğŸ­ (Arrow C) âŸ¶ imFunc :=
{
  /-
    Construct the morphism on objects using the commutative diagram

    X.left - coimage.Ï€ X.hom â‰« coimageImageComparison X.hom - -> Abelian.image X.hom
      |                                                             |
      | X.hom                                                       | Abelian.image.Î¹ X.hom
      v                                                             v
      X.right - - - - - - - - - - ğŸ™ X.right - - - - - - - - -  > X.right
  -/
  app := fun X => Arrow.homMk' (coimage.Ï€ X.hom â‰« coimageImageComparison X.hom) (ğŸ™ X.right)
  naturality := by
    intro X Y f
    ext
    Â· simp[â† cancel_mono (Abelian.image.Î¹ Y.hom)]
    Â· simp
}

end images

section coimages
open Abelian
open Limits
@[simp]
private lemma kernel_to_coimage_comp_eq_zero [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  kernel.Î¹ X.hom â‰« (f.left â‰« coimage.Ï€ Y.hom) = 0 := by
    rw[â† cancel_mono (coimageImageComparison Y.hom â‰« Abelian.image.Î¹ Y.hom)]
    simp


@[simp]
private lemma cokernel.lift_unique [Abelian C] {X Y Z : C} {f : X âŸ¶ Y}
  (z : Y âŸ¶ Z)
  (h_z : f â‰« z = 0)
  (w : cokernel f âŸ¶ Z)
  (h_w : cokernel.Ï€ f â‰« w = z) : w = cokernel.desc f z h_z :=
   (cokernelIsCokernel f).uniq (CokernelCofork.ofÏ€ z h_z) w (by rintro (_ | _) <;> simp[h_z,h_w])

noncomputable def coimage.map [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  Abelian.coimage X.hom âŸ¶ Abelian.coimage Y.hom :=
    cokernel.map
      (kernel.Î¹ X.hom)
      (kernel.Î¹ Y.hom)
      (kernel.map X.hom Y.hom f.left f.right (by simp))
      f.left
      (by simp)

@[simp]
private lemma coimage.naturality [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
   Abelian.coimage.Ï€ X.hom â‰« coimage.map f  = f.left â‰« Abelian.coimage.Ï€ Y.hom :=
    cokernel.Ï€_desc _ _ _

private lemma coimage.map_unique [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y)
  (g : Abelian.coimage X.hom âŸ¶ Abelian.coimage Y.hom)
  (h_f : Abelian.coimage.Ï€ X.hom â‰« g = f.left â‰« Abelian.coimage.Ï€ Y.hom) : g = coimage.map f :=
    cokernel.lift_unique _ _ _ h_f

@[simp]
private lemma coimage.id_eq_id_image [Abelian C] (X : Arrow C) :
  ğŸ™ (Abelian.coimage X.hom) = coimage.map (ğŸ™ X) :=
    coimage.map_unique (ğŸ™ X) _ (by simp)

@[simp]
private lemma coimage.map_comp [Abelian C] {X Y Z : Arrow C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
 coimage.map f â‰« coimage.map g = coimage.map (f â‰« g) := by
  apply coimage.map_unique (f â‰« g)
  simp[Eq.symm (assoc (coimage.Ï€ X.hom) (coimage.map f) (coimage.map g))]

noncomputable def coimFunc [Abelian C] : Arrow C â¥¤ Arrow C where
  obj := fun X : Arrow C => Arrow.mk (Abelian.coimage.Ï€ X.hom)
  map := fun {_ _} f => Arrow.homMk' f.left (coimage.map f)
  map_id := by intro; ext <;> simp
  map_comp := by
    intro X Y Z f g
    ext <;> simp

@[simp]
private lemma coimFunc_obj_hom_eq [Abelian C] (X : Arrow C) :
   (coimFunc.obj X).hom = Abelian.coimage.Ï€ X.hom := rfl

@[simp]
private lemma coimFunc_map_left_eq [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  (coimFunc.map f).left = f.left := rfl

@[simp]
private lemma coimFunc_map_right_eq [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  (Arrow.rightFunc : Arrow C â¥¤ C).map (coimFunc.map f) = coimage.map f := rfl


noncomputable def coimFunc_to_id [Abelian C] : coimFunc âŸ¶ ğŸ­ (Arrow C) :=
{
  /-
    Construct the morphism on objects using the commutative diagram
    X.left - - - - - - - - - - ğŸ™ X.left - - - - - - - - - - - > X.left
      |                                                           |
      | X.coimage.Ï€ X.hom                                         | X.hom
      v                                                           v
    Abelian.coimage X.hom - coimageImageComparison X.hom â‰« - -> X.right
                            Abelian.image.Î¹ X.hom
  -/
  app := fun X => Arrow.homMk' (ğŸ™ X.left) (coimageImageComparison X.hom â‰« Abelian.image.Î¹ X.hom)
  naturality := by
    intro X Y f
    ext
    Â· simp[comp_id f.left,id_comp f.left]
    Â· simp[â† cancel_epi (Abelian.coimage.Ï€ X.hom)]
      calc cokernel.Ï€ (kernel.Î¹ X.hom) â‰«
        (coimFunc.map f).right â‰«
        coimageImageComparison Y.hom â‰«
        kernel.Î¹ (cokernel.Ï€ Y.hom)
        _= Abelian.coimage.Ï€ X.hom â‰«
          coimage.map f â‰«
          coimageImageComparison Y.hom â‰«
          kernel.Î¹ (cokernel.Ï€ Y.hom) := rfl
        _= (Abelian.coimage.Ï€ X.hom â‰« coimage.map f)
          â‰« coimageImageComparison Y.hom
          â‰« kernel.Î¹ (cokernel.Ï€ Y.hom) := by
          exact Eq.symm
            (assoc (coimage.Ï€ X.hom)
            (coimage.map f)
            (coimageImageComparison Y.hom â‰« kernel.Î¹ (cokernel.Ï€ Y.hom)))
        _= f.left â‰« Abelian.coimage.Ï€ Y.hom â‰« coimageImageComparison Y.hom â‰«
          kernel.Î¹ (cokernel.Ï€ Y.hom) := by simp
        _= f.left â‰«
          (Abelian.coimage.Ï€ Y.hom â‰« coimageImageComparison Y.hom â‰« Abelian.image.Î¹ Y.hom) := rfl
        _= f.left â‰« Y.hom := by rw[coimage_image_factorisation Y.hom]
        _= X.hom â‰«f.right := Arrow.w f
}

end coimages

@[simp]
lemma coim_to_im [Abelian C] {X Y : Arrow C} (f : X âŸ¶ Y) :
  Arrow.rightFunc.map (coimFunc.map f) â‰«
  coimageImageComparison Y.hom = coimageImageComparison X.hom â‰«
  Arrow.leftFunc.map (imFunc.map f) := by
    simp only [coimFunc_map_right_eq,imFunc_map_left_eq,
      â† cancel_mono (Abelian.image.Î¹ Y.hom),
      â† cancel_epi (Abelian.coimage.Ï€ X.hom)]
    calc
      coimage.Ï€ X.hom â‰«
      (coimage.map f â‰«
      coimageImageComparison Y.hom) â‰«
      Abelian.image.Î¹ Y.hom
      _= (coimage.Ï€ X.hom â‰«
      coimage.map f) â‰«
      (coimageImageComparison Y.hom â‰«
      Abelian.image.Î¹ Y.hom)  := by simp only [assoc]
      _= (f.left â‰« coimage.Ï€ Y.hom) â‰«
      (coimageImageComparison Y.hom â‰«
      Abelian.image.Î¹ Y.hom)  := by
        simp
      _= f.left â‰« (coimage.Ï€ Y.hom â‰«
      (coimageImageComparison Y.hom â‰«
      Abelian.image.Î¹ Y.hom)) := by simp only[assoc]
      _= f.left â‰« Y.hom := by simp
      _= X.hom â‰« f.right := by simp
      _= (coimage.Ï€ X.hom â‰«
        coimageImageComparison X.hom â‰«
        image.Î¹ X.hom)
        â‰« f.right := by simp
      _= coimage.Ï€ X.hom â‰«
          (coimageImageComparison X.hom â‰«
          image.map f) â‰«
          Abelian.image.Î¹ Y.hom := by simp

noncomputable def coimage_image_factor [Abelian C] :
  Arrow C â¥¤ Comma (Arrow.rightFunc : Arrow C â¥¤ C) (Arrow.leftFunc : Arrow C â¥¤ C) where
    obj := fun (X : Arrow C) =>
      Comma.mk (coimFunc.obj X) (imFunc.obj X) (coimageImageComparison X.hom)
    map := fun {X Y : Arrow C} (f : X âŸ¶ Y) =>
      CommaMorphism.mk (coimFunc.map f) (imFunc.map f) (coim_to_im f)
    map_id := fun X => by ext <;> simp
    map_comp := fun {X Y Z} f g => by ext <;> simp

@[simp]
lemma coimage_image_factor_obj_left_eq [Abelian C] (X : Arrow C) :
  (coimage_image_factor.obj X).left = coimFunc.obj X := rfl

@[simp]
lemma coimage_image_factor_obj_right_eq [Abelian C] (X : Arrow C) :
  (coimage_image_factor.obj X).right = imFunc.obj X := rfl

@[simp]
lemma coimage_image_factor_hom_eq [Abelian C] (X : Arrow C) :
  (coimage_image_factor.obj X).hom = coimageImageComparison X.hom := rfl

@[simp]
lemma left_obj_eq_left [Abelian C] (X : Arrow C) :
  (coimage_image_factor.obj X).left.left = X.left := rfl
@[simp]
lemma right_obj_eq_right [Abelian C] (X : Arrow C) :
  (coimage_image_factor.obj X).right.right = X.right := rfl

noncomputable def functorial_coim_im_factorisation [Abelian C] : FunctorialFactorization C where
  factor := coimage_image_factor
  factor_left_obj := left_obj_eq_left
  factor_right_obj := right_obj_eq_right
  factorization_spec := by simp
  factor_middle_iso := fun X => by
    simp only[coimage_image_factor_hom_eq, instIsIsoCoimageImageComparison]
end FunctorialFactorization

end CategoryTheory
